plugins {
    id 'org.springframework.boot' version '2.2.5.RELEASE'
    id 'io.spring.dependency-management' version '1.0.9.RELEASE'
    id 'java'
    id "io.freefair.lombok" version "5.0.0-rc6"
}

group = 'no.fint'
sourceCompatibility = '11'

repositories {
    maven {
        url 'https://repo.fintlabs.no/releases'
    }
    mavenCentral()
    mavenLocal()
}

dependencies {
    implementation('org.springframework.boot:spring-boot-starter-webflux')
    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }
    implementation('no.fint:fint-audit-api:+')
    implementation('com.azure:azure-storage-blob:+')
    implementation('com.google.guava:guava:+')
    implementation('org.projectreactor:reactor-core:+')
}

test {
    useJUnitPlatform()
}


task copyExternalDependencies(type: Copy) {
    from configurations.runtimeClasspath
    into "$buildDir/deps/external"
    exclude '**/fint-*.jar'
}

task copyFintDependencies(type: Copy) {
    from configurations.runtimeClasspath
    into "$buildDir/deps/fint"
    include '**/fint-*.jar'
}

build.dependsOn(copyExternalDependencies)
build.dependsOn(copyFintDependencies)

jar {
    enabled(true)
    manifest {
        attributes(
                "Manifest-Version": "1.0",
                "Main-Class": "no.fint.Application",
                "Specification-Version": "${archiveVersion}",
                "Implementation-Version": "${archiveVersion}",
                "Class-Path": configurations.runtimeClasspath.collect { it.getName() }.join(' ')
        )
    }
}

bootJar {
    enabled(false)
}
